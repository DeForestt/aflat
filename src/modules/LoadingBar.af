
.needs <std>
import { worker, makeShared } from "concurrency" under async;
import AsyncResult from "concurrency";
import { print } from "String" under str;
import string from "String";

private adr runLoadingBar(adr bar);

class LoadingBar {
	int max = max;
	mutable int resolution = resolution;
	private mutable int current = 0;
	private mutable bool kill = false;
	private mutable bool updated = true;
    private mutable AsyncResult result;

	LoadingBar init(int max, * int resolution) {
		if resolution == 0 my.resolution = 20;
		return my;
	};

	void update() {
		my.current = my.current + 1;
		my.updated = true;
	};

	void print() {
		if !my.updated {
			return;
		};
		my.updated = false;

		// map current to resolution
		int current = my.current * my.resolution / my.max;
		
		mutable let strBuff = `\rLoading: [`;
		for int i = 0; i < my.resolution; i++ {
			if i < current {
				strBuff = `{strBuff}#`;
			} else {
				strBuff = `{strBuff} `;
			};
		};
		strBuff = `{strBuff}] {my.current * 100 / my.max}% `;
		str.print(strBuff);
	};

	void kill() {
		my.kill = true;
        if my.result != NULL {
            my.result.await();
        };
	};

	bool isKilled() {
		return my.kill;
	};

    void run() {
        my.result = runLoadingBar(my);
    };
};

export adr createLoadingBar(int max, * int resolution) {
	return async.makeShared(LoadingBar, LoadingBar.init, max, resolution);
};

private any runLoadingBar(LoadingBar bar) : async.worker {
	while true {
		bar.print();
		if bar.isKilled() {
			break;
		};
	};
	return NULL;
};



