.needs <std>

import string from "String";
import List from "Collections";
import {fPrint} from "io" under io;

class Arg {
    string name = "";
    string value = "";

    Arg init(string name, string value) {
        my.name = name;
        my.value = value;
    };
};

class ArgsList {
    List args = new List(Arg);
    List tagLessArgs = new List(string);
    string progName = "";

    ArgsList init(int argc, adr argv, adr tagNames) {
        const adr[0] args = argv;
        const adr[0] tagVals = tagNames;
        List tags = new List(string);

        int j = 0;
        adr tv = tagVals[j];
        while (tv != NULL) {
            string v = tagVals[j];
            tags.pushBack(v);
            j++;
            tv = tagVals[j];
        };

        my.progName = args[0];

        for int i = 1; i < argc; i++ {
            string arg = args[i];
            if (arg.at(0) == '-') {
                let subArg = arg.subString(1, arg.len());
                if (tags.findFirst([string s, string y] => return s == y, subArg) != NULL) {

                    if (i + 1 < argc) {
                        string val = args[i + 1];
                        my.args.pushBack(Arg(subArg, val));
                        i++;
                    } else {
                        let val = new string("");
                        let ag = Arg(subArg, val);
                        my.args.pushBack(ag);
                    };
                } else {
                    io.fPrint("Error: Unknown tag: %s\n", {subArg});
                    return NULL;
                };
            } else {
                my.tagLessArgs.pushBack(arg.copy());
            };
        };

        return my;
    };

    string getArg(string name) {
        Arg arg = my.args.findFirst([Arg a, string n] => return a.name == n, name);
        if (arg != NULL) {
            return arg.value;
        } else {
            return new string("");
        };
    };

    string getTagLessArg(int index) {
        if (index < my.tagLessArgs.getCount()) {
            return my.tagLessArgs.get(index);
        } else {
            return new string("");
        };
    };

    bool has(string name) {
        return my.args.findFirst([Arg a, string n] => return a.name == n, name) != NULL;
    };
};
