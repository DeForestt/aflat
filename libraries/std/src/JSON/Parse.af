.needs <std>
import string from "String";
import { fString, print } from "String" under st;
import { fPrint } from "io" under io;
import List from "Collections";
import result from "Utils/result";

/*
 * Take in a string and a json string and return a string
 * value.
 */
export string getString(const string key, const string json) {
    const let start = json.find(key, 0) + key.len() + 2;
    const let newString = json.subString(start, json.len());
    const let newStart = newString.find("\"", 0);
    const let newEnd = newString.find("\"", newStart + 1);
    const let value = newString.subString(newStart + 1, newEnd);
    return value;
};

/*
 * Take in a string and a json string and return a int
 * value.
 */
export int getInt(const string key, const string json) {
    const let start = json.find(key, 0) + key.len() + 2;
    mutable let end = json.find(",", start);
    if end == -1 {
        end = json.find("}", start);
    };
    mutable let value = json.subString(start, end);
    value = value.trim(' ');

    match value.toInt() {
        Ok(val) => return val,
        Err() => return -1
    };
};

/*
 * Take in a string and a json string and return a json string
 * value.  ***NOTE:*** This function is not complete.  It will
 * not work for nested json objects.
 */
export string getJson(const string key, const string json) {
    const let start = json.find(key, 0) + key.len() + 2;
    const let newString = json.subString(start, json.len());
    const let newStart = newString.find("{", 0);
    const let afterOpen = newString.subString(newStart + 1, newString.len());
    int openCount = 1;
    int closeCount = 0;

    for int i = 0; i < afterOpen.len(); i = i + 1 {
        const char c = afterOpen.at(i);
        if c == '{' {
            openCount = openCount + 1;
        } else if c == '}' {
            closeCount = closeCount + 1;
        };
        if openCount == closeCount {
            const let value = afterOpen.subString(0, i);
            return st.fString("{%s}", {value});
        };
    };
    return new string("undefined");
};

/*
 * Consume a key and a json string and return a list of
 * strings.
 */
export List getList(const string key, const string json) {
    const let start = json.find(key, 0) + key.len() + 2;
    const let newString = json.subString(start, json.len());
    const let newStart = newString.find("[", 0);
    const let afterOpen = newString.subString(newStart + 1, newString.len());
    int openCount = 1;
    int closeCount = 0;

    mutable string listBuffer = new string("");
    const List ret = new List(string);
    
    const let flush = fn (mutable string s) {
        if s.len() > 0
            s = "";
    };

    mutable let inQuote = false;
    mutable let inObject = false;
    

    for int i = 0; i < afterOpen.len(); i = i + 1 {
        const char c = afterOpen.at(i);
        if c == '[' {
            openCount = openCount + 1;
        } else if c == ']' {
            closeCount = closeCount + 1;
        };

        if (c == '"') & (openCount - closeCount == 1) & (!inObject) {
            inQuote = !inQuote;
        };

        if (c == '{') & (openCount - closeCount == 1) {
            inObject = true;
        };

        if (c == '}') & (openCount - closeCount == 1) {
            inObject = false;
        };


        if (openCount - closeCount == 1) & (!inQuote) & (!inObject) {
            if c == ',' {
                ret.pushBack(listBuffer.trim(' '));
                flush(listBuffer);
            } else {
                listBuffer = listBuffer.push(c);
            };
        } else {
            listBuffer = listBuffer.push(c);
        };

        if openCount == closeCount {
            if listBuffer.at(listBuffer.len() - 1) == ']'
                listBuffer = listBuffer.subString(0, listBuffer.len() - 1);
            ret.pushBack(listBuffer.trim(' '));
            return ret;
        };
    };
    return ret;
};
