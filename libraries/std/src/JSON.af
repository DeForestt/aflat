.needs <std>
import string from "String";
import result from "Utils/result";
import option from "Utils/option";
import vector from "Collections/Vector";
import unordered_map from "Collections/unordered_map";
import {Some, None, optionWrapper} from "Utils/option" under opt;
import {accept, reject, resultWrapper} from "Utils/result" under res;
import {print} from "String" under str;
<<<<<<< Updated upstream
import {char_isDigit, char_isWhiteSpace} from "strings" under st;
=======
import {len} from "strings" under st;
>>>>>>> Stashed changes
import Box from "Memory";
import {wrap} from "Memory" under box;
import Error from "Utils/Error";

union JSON {
    String(adr),
    Number(int),
    Boolean(bool),
    List(vector::<Box::<JSON>>),
    Object(unordered_map::<Box::<JSON>>),
    Null

    fn get(const string key) -> JSON? {
        match my {
            Object(map) => {
                match map.get(key.cstr()) {
                    Some(value) => return value.get(), // unwrap the Box
                    None => return NULL // null will become None
                }
            },
            _ => return NULL,
        };
    };

    fn set(const string key, const JSON value) -> JSON! {
        match my {
            Object(map) => {
                map.set(key.cstr(), new Box::<JSON>(value));
                return my;
            },
            _ => return new Error("JSON is not an Object")
        }
    };

    fn asString() -> adr! {
        match my {
            String(s) => return s,
            _ => return new Error("JSON is not a String")           
        }
    };

    fn asNumber() -> int! {
        match my {
            Number(n) => return n,
            _ => return new Error("JSON is not a Number")
        }
    };

    fn asBoolean() -> bool! {
        match my {
            Boolean(b) => return b,
            _ => return new Error("JSON is not a Boolean")
        }
    };

    fn asList() -> vector::<Box::<JSON>>! {
        match my {
            List(list) => return list,
            _ => return new Error("JSON is not a List"),
        }
    }; 

    fn asObject() -> unordered_map::<Box::<JSON>>! {
        match my {
            Object(map) => return map,
            _ => return new Error("JSON is not an Object")
        }
    };

    fn isNull() -> bool {
        match my {
            Null => return true,
            _ => return false
        }
    };

    fn toString() -> string {
        match my {
            String(s) => return `String({s})`,
            Number(n) => return `Number({n})`,
            Boolean(b) => return `Boolean({b})`,
            List(list) => return `List({list})`,
            Object(map) => return `Object({map})`,
            Null => return "Null"
        };
    };

    private fn wrap(const string s, const char c1, const char c2) -> string {
        return `{c1}{s}{c2}`;
    };

    fn stringify(?const int indent_) -> string {
        const let indent = 1 * indent_.or(0); // If 0, there will never be any indentation
        const let nextIndent = if indent > 0 indent + 1 else 0;
        const let nl = if indent > 0 "\n" else "";
        mutable string indentStr = "";
        for int i = 0; i < indent; i++ {
            indentStr = `{indentStr}  `;
        };
        
        match my {
            String(s) => {
                const let value = my.wrap(s, '"', '"');
                return `{indentStr}{value}`;
            },
            Number(n) => return `{indentStr}{n}`,
            Boolean(b) => return `{indentStr}{b}`,
            List(list) => {
                const let v = match list.next() {
                    Some(value) => resolve value.get(), // unwrap the Box
                    None => return "[]"
                };
                mutable let builder = `[{nl}{indentStr}{v.stringify(nextIndent)}`;
                foreach item in list {
                    const let item = item.get(); // unwrap the Box

                    builder =  `{builder},{nl}{indentStr}{item.stringify(nextIndent)}`;
                };
                return `{builder}{nl}{indentStr}]`;
            },
            Object(map) => {
                mutable string builder = `{nl}{indentStr}`;
                let first = true;
                
                const let keys = map.keys();
                foreach key in keys {
                    const let value = map.get(key).expect(`Value {key} should be present`);
                    // value is a Box, so we need to unwrap it
                    const let value = value.get();
                    const let wrapKey = my.wrap(key, '"', '"');
                    if first {
                        first = false;
                    } else {
                        builder = `{builder},{nl}{indentStr}`;
                    };
                    builder = `{builder}{wrapKey}:`;
                    builder = builder + value.stringify(nextIndent);
                };
                mutable string sub1Indent = "";
                for int i = 0; i < indent - 1; i++ {
                    sub1Indent = `{sub1Indent}  `;
                };
                builder = `{builder}{nl}{indentStr}`;
                return my.wrap(builder, '{', '}');
            },
            Null => return `null`
        };


    };
};

export fn Object(const unordered_map::<Box::<JSON>> map) -> JSON {
    return new JSON->Object(map);
};

export fn String(const adr s) -> JSON {
    if s == NULL return new JSON->Null();
    let v = malloc(st.len(s) + 1);
    memcpy(v, s, st.len(s) + 1);
    return new JSON->String(v);
};

export fn Number(const int n) -> JSON {
    return new JSON->Number(n);
};

export fn Boolean(const bool b) -> JSON {
    return new JSON->Boolean(b);
};

export fn List(const vector::<Box::<JSON>> list) -> JSON {
    return new JSON->List(list);
};

export fn Null() -> JSON {
    return new JSON->Null();
};

private fn parseInternal(const string json_) -> JSON!;

private fn parseStringValue(const string value) -> JSON! {
    string builder = "";
    foreach c in value {
        if c == '\"' {
            return String(builder.cstr());
        };
        builder = `{builder}{c}`;
    };
    return new Error("Unterminated string in JSON");
};

private fn parseBooleanValue(const string value) -> JSON! {
    if value.consume("rue") {
        return Boolean(true);
    } else if value.consume("alse") {
        return Boolean(false);
    };
    return new Error(`Invalid boolean in JSON: {value}`);
};

private fn parseNullValue(const string value) -> JSON! {
    if value.consume("ull") {
        return Null();
    };
    return new Error(`Invalid null in JSON: {value}`);
};

private fn parseArrayValue(const string value) -> JSON! {
    const let elements = new vector::<Box::<JSON>>();
    let v = value.peek();
    while true {
        const let parsed = parseInternal(value)!;
        const let boxed = box.wrap(parsed);
        elements.push_back($boxed);
        v = value.peek();
        if `{v.or('v')}` == "," {
            value.next();
        } else if `{v.or('v')}` == "]" {
            break;
        };
    };
    value.next();
    return new JSON->List(elements);
};

private fn parseObjectValue(const string value) -> JSON! {
    const let map = new unordered_map::<Box::<JSON>>();
    let v = value.peek();
    while true {
        if v.or('v') == '}' {
            value.next();
            return new JSON->Object(map);
        };

        const let key = match parseInternal(value)!.asString() {
            Ok(s) => s,
            Err(e) => return new Error(`Expected string key in JSON object: {e}`)
        };
        const let colon = value.next().or('n');
        if !(`{colon}` == ":") {
            return new Error(`Expected ':' after key in JSON object: {value} but found '{colon}'`);
        };
        const let parsedVal = parseInternal(value)!;
        map.set(key, box.wrap(parsedVal));
        while st.char_isWhiteSpace(value.peek().or('n')) v = value.next();
        v = value.peek();
        if `{v.or('v')}` == "," {
            value.next();
            while st.char_isWhiteSpace(value.peek().or('n')) v = value.next();
            v = value.peek();
        } else if `{v.or('v')}` == "}" {
            value.next();
            return new JSON->Object(map);
        } else {
            return new Error(`Expected ',' or '}' in JSON object: {value}`);
        };
    };
};

private fn parseInternal(const string json_) -> JSON! {
    foreach c in json_ {
        if st.char_isWhiteSpace(c) continue;
        if c == '\"' return parseStringValue(json_);
        if st.char_isDigit(c) {
            string numStr = `{c}`;
            while st.char_isDigit(json_.peek().or('v')) {
                match json_.next() {
                    Some(nc) => numStr = `{numStr}{nc}`,
                    None => break
                };
            };
            match numStr.toInt() {
                Ok(num) => return Number(num),
                Err(e) => return new Error(`Invalid number in JSON: {numStr} - {e}`)
            };
        };
        if c == 't' | c == 'f' return parseBooleanValue(json_);
        if c == 'n' return parseNullValue(json_);
        if c == '[' return parseArrayValue(json_);
        if c == '{' return parseObjectValue(json_);

        return new Error(`Unsupported JSON value: {json_} : {c}`);
    };
    return new Error(`JSON is empty or is not valid: {json_}`);
};

export fn parse(const string jsonStr) -> JSON! {
    return parseInternal(jsonStr);
};
