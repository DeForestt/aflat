 .needs <Collections.gs>
 .needs <std.gs>
 .needs <io>

/*
 * function name: NewList;
 * description:   create a new list;
 * arguments:     none;
 * returns:       a new list;
 */
 LinkedList newList(){
    LinkedList list = malloc(LinkedList);
    list.init();
    return list;
 };

 /*
 * function name:   forEach@LinkedList;
 * description:     iterates through the list and calls the given function on each node
                    it will break if the function returns 1;
 * agruments:       adr foo : the function to be called on each node
 *                  adr arg : the argument to be passed to the function;
 * returns:         int : 0 if successful, -1 if not;
 */
int forEachLinkedList(LinkedList my, adr foo, adr arg){
    ListNode curr;
    curr = my.head;
    int i = 0;
    while(curr != NULL){
        adr data;
        data = curr.data;
        int cont = foo(data, i, arg);
        if(cont == 1){
            return 1;
        };
        curr = curr.next;
        i = i + 1;
    };
    return 0;
};

LinkedList whereLinkedList(LinkedList my, adr foo, adr arg){
    LinkedList newList = new LinkedList();
    ListNode curr;
    curr = my.head;
    int i = 0;
    while(curr != NULL){
        adr data;
        data = curr.data;
        bool boo = foo(data, i, arg);
        if(boo == true){
            newList.append(data);
        };
        curr = curr.next;
        i = i + 1;
    };
    return newList;
};

/*
 * function name: init@LinkedList;
 * description:   initialize a list;
 * arguments:     none;
 * returns:       none;
 */

LinkedList init@LinkedList(){
    my.head = NULL;
    my.forEach = forEachLinkedList;
    my.where = whereLinkedList;
    return my;
};

/*
 * function name: newNode;
 * description:   create a new node;
 * arguments:     none;
 * returns:       a new node;
 */

ListNode newNode(){
    ListNode node = malloc(ListNode);
    node.data = NULL;
    return node;
};

/*
 * function name:   remove@LinkedList;
 * description:     removes the node at the given index;
 * agruments:       int index : the index of the node to remove;
 * returns:         int : 0 if successful, -1 if not;
 */

int delete@LinkedList(int index){
    if index < 0 | index > my.size() {
        return -1;
    };

    ListNode curr = my.head;

    if index == 0 {
        my.head = curr.next;
        return 0;
    };

    int i = 0;
    while(curr != NULL){
        if i == index - 1 {
            ListNode next = curr.next;
            curr.next = next.next;
            return 0;
        };
        curr = curr.next;
        i = i + 1;
    };
    return -1;
};

int stitch@LinkedList(LinkedList ln){
    ListNode curr;
    curr = my.head;
    while(curr.next != NULL){
        curr = curr.next;
    };
    curr.next = ln.head;
    return 0;
};

/*
 * function name:   get@LinkedList;
 * description:     returns the node at the given index;
 * agruments:       int index : the index of the node to return;
 * returns:         adr : the node at the given index;
 */

adr get@LinkedList(int index){
    if (index < 0){
        return NULL;
    };
    ListNode myHead;
    myHead = my.head;
    ListNode myIndex;
    myIndex = myHead;

    int i = 0;
    while(i < index){
        myIndex = myIndex.next;
        i = i + 1;
    };

    return myIndex.data;
};

/*
 * function name:   append@LinkedList;
 * description:     appends a node to the end of the list;
 * agruments:       adr data : the data to be appended;
 * returns:         int : 0 if successful, -1 if not;
 */

int append@LinkedList(adr data){
    ListNode ln = new ListNode();
    ln.data = data;
    ln.next = NULL;

    ListNode head = my.head;

    if(head == NULL){
        my.head = ln;
        return 0;
    };

    ListNode curr = my.head;
    ListNode next = curr.next;
    while(next != NULL){
        curr = next;
        next = curr.next;
    };

    curr.next = ln;
    return 0;
};

/*
 * function name:   insert@LinkedList;
 * description:     inserts a node at the given index;
 * agruments:       int index : the index to insert the node at
 *                  adr data : the data to be inserted;
 * returns:         int : 0 if successful, -1 if not;
 */
int insert@LinkedList(adr data, int index){
    if (index < 0 | index > my.size()){
        return -1;
    };

    ListNode ln = new ListNode();
    ln.data = data;
    ln.next = NULL;


    if (index == 0){
        ln.next = my.head;
        my.head = ln;
        return 0;
    };

    ListNode curr;
    curr = my.head;
    
    for int i = 0; i < index - 1 ; i = i + 1
        curr = curr.next;
    
    ListNode next = curr.next;
    curr.next = ln;
    ln.next = next;
    return 0;
};

/*
 * function name:   size@LinkedList;
 * description:     returns the size of the list;
 * agruments:       none
 * returns:         int : the size of the list;
 */

int size@LinkedList(){
    ListNode curr;
    curr = my.head;
    int i = 0;
    while (curr != NULL) {
        curr = curr.next;
        i = i + 1;
    };
    return i;
};

LinkedList sort@LinkedList(adr foo, adr arg){
    LinkedList n = new LinkedList();
    ListNode curr = my.head;
    while(curr != NULL){
        adr data = curr.data;
        int i = 0;
        bool loop = true;
        while(i < n.size() & loop){
            adr data2 = n.get(i);
            bool cmp = foo(data, data2, arg);
            if(cmp == true){
                n.insert(data, i);
                loop = false;
            };
            i = i + 1;
        };
        if(i == n.size()){
            n.append(data);
        };
        curr = curr.next;
    };
    return n;
};


LinkedList map@LinkedList(adr foo, adr arg){
    LinkedList newList = new LinkedList();
    ListNode curr = my.head;
    int i = 0;
    while(curr != NULL){
        adr data;
        data = curr.data;
        adr newData;
        newData = foo(data, i, arg);
        newList.append(newData);
        curr = curr.next;
        i = i + 1;
    };
    return newList;
};

LinkedList copy@LinkedList(LinkedList list){
    my.erace();
    return list;
};


/*
 * function name:   forEach@Array;
 * description:     preforms a function for each element of the array passed by value
                    it will break if the function returns 1;
 * arguments:       foo : a function pointer to call on the elements
                    arg : an argument pointer;
 * returns:         int: 0 if sucssesfull, 1 on error
 */
int forEachArray(Array my, adr foo, adr arg){
    int size;
    size = my.size;
    adr element;
    int tSize;
    tSize = my.typeSize;
    adr curr;

    int i;
    for i = 0; i < size; i = i + 1 {
        int offset = i * tSize;
        curr = my.head;
        curr = curr + offset;
        int brk = foo(curr, i, arg);
        if (brk == 1){
            return 0;
        };
    };
    return 0;
};

int find@LinkedList(adr foo, adr arg){
    ListNode curr;
    curr = my.head;
    int i = 0;
    while(curr != NULL){
        adr data;
        data = curr.data;
        bool brk = foo(data, i, arg);
        if (brk == true){
            return i;
        };
        curr = curr.next;
        i = i + 1;
    };
    return -1;
};

Array whereArray(Array my, adr foo, adr arg){
    Array a = new Array(adr, 0);
    int size;
    int tSize;
    tSize = my.typeSize;
    size = my.size;
    int i;
    for i = 0; i < size; i = i + 1 {
        int offset = i * tSize;
        adr curr;
        curr = my.head;
        curr = curr + offset;
        adr save; save = curr;
        bool boo = foo(curr, i, arg);
        if (boo == true){
            a.extend(1);
            int msize;
            msize = a.size;
            msize = msize - 1;
            adr point = a.at(msize);
            point =: save;
        };
    };
    return a;
};

/*
 * function name: init@Array;
 * description:   initialize an array;
 * arguments:     int typeSize : the size of the type
 *                int size : the size of the array;
 * returns:       0 if successful, -1 if not;
 */
Array init@Array(int typeSize, int size){
    my.size = size;
    my.typeSize = typeSize;
    
    size = size * typeSize;
    my.head = malloc(size);
    my.forEach = forEachArray;
    my.where = whereArray;
    return my;
};

/*
 * function name:   at@Array;
 * description:     returns the address of the element at the given index;
 * agruments:       int index : the index of the element to return;
 * returns:         adr : the address of the element at the given index;
 */
adr at@Array(int index){
    int tSize;
    tSize = my.typeSize;
    int offset = index * tSize;
    adr curr;
    curr = my.head;
    curr = curr + offset;
    return curr;
};

/*
  * function name: newArray;
  * description:   create a new array;
  * arguments:     int typeSize : the size of the type
  *                int size : the size of the array;
  * returns:       Array : a new array
  */
Array newArray(int typeSize, int size){
    Array my = malloc(Array);
    my.init(typeSize, size);
    return my;
};

/*
 * function name:   delete@Array;
 * description:     deletes the array and frees the memory;
 * arguments:       none;
 * returns:         int :  0;
 */
int delete@Array(){
    free(my.head);
    free(my);
    return 0;
};

/*
 * function name:   free@LinkedList;
 * description:     frees the memory of the list;
 * arguments:       none;
 * returns:         int :  0;
 */
int erace@LinkedList(){
    ListNode curr;
    curr = my.head;
    while(curr != NULL){
        ListNode temp;
        temp = curr.next;
        free(curr.data);
        free(curr);
        curr = temp;
    };
    free(my);
    return 0;
};

LinkedList reverse@LinkedList(){
    // flip all of the node pointers
    ListNode curr;
    curr = my.head;
    ListNode prev;
    prev = NULL;
    while(curr != NULL){
        ListNode temp;
        temp = curr.next;
        curr.next = prev;
        prev = curr;
        curr = temp;
    };
    my.head = prev;
    return my;
};

/*
 * function name:   extend@Array;
 * description:     extends the array by the given amount;
 * arguments:       int amount : the amount to extend the array by;
 * returns:         int size : the new size of the array;
 */
int extend@Array(int amount){
    adr curr;
    curr = my.head;
    int currSize;
    currSize = my.size;
    my.size = currSize + amount;

    int tSize;
    tSize = my.typeSize;

    amount = amount + currSize;
    amount = amount * tSize;

    my.head = realloc(curr, amount);
    if(my.head == NULL){
        print("badrealloc");
    };
    return  amount;
};