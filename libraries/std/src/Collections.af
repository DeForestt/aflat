.needs <std>

import * from "io" under io;

class ICollection{
    contract {
        adr forEach = [] => panic("Function (forEach) not implemented on this instance");
        adr where = [] => panic("Function (where) not implemented on this instance");
    };
};

class ListNode{
    adr next;
    adr data;
};

class LinkedList {
    public ListNode head;
    public adr pos = my.head;
    private adr delFunc = delFunc;
    bool cleanUp = cleanUp;
    LinkedList init(* adr delFunc, * bool cleanUp);
    int append(adr data);
    int insert(adr data, int index);
    int drop(int index);
    generic get(int index);

    adr stitch(LinkedList New);
    LinkedList reverse();
    LinkedList where(adr foo, * adr args);
    int forEach(adr foo, * adr args);
    LinkedList sort(adr compare, * adr args);
    adr reduce(adr foo, * adr args);
    int find(adr foo, * adr args);
    adr apply(adr foo, * adr args);
    adr map(adr foo, * adr args);
    int size();
    int erace();
    public LinkedList copy<<=>>(adr list);
    int del(){
        if (my.delFunc != NULL) {
            adr foo = my.delFunc;
            my.forEach(foo);
        };
        if (my.cleanUp) {
            my.forEach(free);
        };
        ListNode curr = my.head;
        ListNode next = NULL;
        while (curr != NULL) {
            next = curr.next;
            free(curr);
            curr = next;
        };
    };

};

class Array signs ICollection{
    public int typeSize;
    public adr head;
    int size;

    Array init(int typeSize, int size);
    public adr at(int index);
    public int del(int index);
    public int extend(int size);
};

class Keyed {
    contract {
        public int key;
    };
};

class BinTree {
    const Keyed data = data;
    private BinTree left = NULL;
    private BinTree right = NULL;

    public BinTree init(Keyed data) {
        return my;
    };

    public bool insert(Keyed data) {
        if data.key == my.data.key return false;

        if data.key < my.data.key {
            if my.left == NULL {
                my.left = new BinTree(data);
                return true;
            } else return my.left.insert(data);
        } else {
            if my.right == NULL {
                my.right = new BinTree(data);
                return true;
            } else return my.right.insert(data);
        };

        return true;
    };

    public adr find(int key) {
        if my.data.key == key return my.data;

        if key < my.data.key {
            if my.left == NULL return NULL
            else return my.left.find(key);
        } else {
            if my.right == NULL return NULL
            else return my.right.find(key);
        };
    };

    public LinkedList traverse() {
        LinkedList list = new LinkedList();
        list.append(my.data);
        if my.left != NULL list.stitch(my.left.traverse());
        if my.right != NULL list.stitch(my.right.traverse());
        return list;
    };

    public int normalize() {
        if my.left != NULL my.left.normalize();
        if my.right != NULL my.right.normalize();

        if my.left != NULL {
            if my.left.right != NULL {
                BinTree temp = my.left.right;
                my.left.right = NULL;
                my.left = temp;
            };
        };

        if my.right != NULL {
            if my.right.left != NULL {
                BinTree temp = my.right.left;
                my.right.left = NULL;
                my.right = temp;
            };
        };

        return 0;
    };
};

adr reduce@LinkedList(adr foo, * adr args){
    adr result = my.head.data;
    ListNode node = my.head.next;
    while(node != NULL){
        result = foo(result, node.data, args);
        node = node.next;
    };
    return result;
};

int forEach@LinkedList(adr foo, * adr arg){
    ListNode curr = my.head;
    int i = 0;
    while(curr != NULL){
        adr data;
        data = curr.data;
        int cont = foo(data, i, arg);
        if(cont == 1){
            return 1;
        };
        curr = curr.next;
        i = i + 1;
    };
    return 0;
};

LinkedList where@LinkedList(adr foo, * adr arg){
    LinkedList newList = new LinkedList();
    ListNode curr = my.head;
    int i = 0;
    while(curr != NULL){
        adr data;
        data = curr.data;
        bool boo = foo(data, i, arg);
        if(boo == true){
            newList.append(data);
        };
        curr = curr.next;
        i = i + 1;
    };
    return newList;
};

LinkedList init@LinkedList(* adr delFunc, * bool cleanUp){
    my.head = NULL;
    return my;
};

int drop@LinkedList(int index){
    if index < 0 | index > my.size() {
        return -1;
    };

    ListNode curr = my.head;

    if index == 0 {
        my.head = curr.next;
        return 0;
    };

    int i = 0;
    while(curr != NULL){
        if i == index - 1 {
            ListNode next = curr.next;
            curr.next = next.next;
            return 0;
        };
        curr = curr.next;
        i = i + 1;
    };
    return -1;
};

int stitch@LinkedList(LinkedList ln){
    ListNode curr;
    curr = my.head;
    while(curr.next != NULL){
        curr = curr.next;
    };
    curr.next = ln.head;
    return 0;
};

generic get@LinkedList(int index){
    if (index < 0){
        return NULL;
    };
    ListNode myHead;
    myHead = my.head;
    ListNode myIndex;
    myIndex = myHead;

    int i = 0;
    while(i < index){
        myIndex = myIndex.next;
        i = i + 1;
    };

    return myIndex.data;
};

int append@LinkedList(adr data){
    ListNode ln = new ListNode();
    ln.data = data;
    ln.next = NULL;

    ListNode head = my.head;

    if(head == NULL){
        my.head = ln;
        return 0;
    };

    ListNode curr = my.head;
    ListNode next = curr.next;
    while(next != NULL){
        curr = next;
        next = curr.next;
    };

    curr.next = ln;
    return 0;
};

int insert@LinkedList(adr data, int index){
    if (index < 0 | index > my.size()){
        return -1;
    };

    ListNode ln = new ListNode();
    ln.data = data;
    ln.next = NULL;


    if (index == 0){
        ln.next = my.head;
        my.head = ln;
        return 0;
    };

    ListNode curr;
    curr = my.head;
    
    for int i = 0; i < index - 1 ; i = i + 1
        curr = curr.next;
    
    ListNode next = curr.next;
    curr.next = ln;
    ln.next = next;
    return 0;
};

int size@LinkedList(){
    ListNode curr;
    curr = my.head;
    int i = 0;
    while (curr != NULL) {
        curr = curr.next;
        i = i + 1;
    };
    return i;
};

LinkedList sort@LinkedList(adr foo, adr arg){
    LinkedList n = new LinkedList();
    ListNode curr = my.head;
    while(curr != NULL){
        adr data = curr.data;
        int i = 0;
        bool loop = true;
        while(i < n.size() & loop){
            adr data2 = n.get(i);
            bool cmp = foo(data, data2, arg);
            if(cmp == true){
                n.insert(data, i);
                loop = false;
            };
            i = i + 1;
        };
        if(i == n.size()){
            n.append(data);
        };
        curr = curr.next;
    };
    return n;
};

LinkedList map@LinkedList(adr foo, adr arg){
    LinkedList newList = new LinkedList();
    ListNode curr = my.head;
    int i = 0;
    while(curr != NULL){
        adr data;
        data = curr.data;
        adr newData;
        newData = foo(data, i, arg);
        newList.append(newData);
        curr = curr.next;
        i = i + 1;
    };
    return newList;
};

LinkedList copy@LinkedList(LinkedList list){
    my.erace();
    return list;
};

private int forEachArray(Array my, adr foo, adr arg){
    int size;
    size = my.size;
    adr element;
    int tSize;
    tSize = my.typeSize;
    adr curr;

    int i;
    for i = 0; i < size; i = i + 1 {
        int offset = i * tSize;
        curr = my.head;
        curr = curr + offset;
        int brk = foo(curr, i, arg);
        if (brk == 1){
            return 0;
        };
    };
    return 0;
};

int find@LinkedList(adr foo, adr arg){
    ListNode curr;
    curr = my.head;
    int i = 0;
    while(curr != NULL){
        adr data;
        data = curr.data;
        bool brk = foo(data, i, arg);
        if (brk == true){
            return i;
        };
        curr = curr.next;
        i = i + 1;
    };
    return -1;
};

private Array whereArray(Array my, adr foo, adr arg){
    Array a = new Array(adr, 0);
    int size;
    int tSize;
    tSize = my.typeSize;
    size = my.size;
    int i;
    for i = 0; i < size; i = i + 1 {
        int offset = i * tSize;
        adr curr;
        curr = my.head;
        curr = curr + offset;
        adr save; save = curr;
        bool boo = foo(curr, i, arg);
        if (boo == true){
            a.extend(1);
            int msize;
            msize = a.size;
            msize = msize - 1;
            adr point = a.at(msize);
            point =: save;
        };
    };
    return a;
};

Array init@Array(int typeSize, int size){
    my.size = size;
    my.typeSize = typeSize;
    
    size = size * typeSize;
    my.head = malloc(size);
    my.forEach = forEachArray;
    my.where = whereArray;
    return my;
};

adr at@Array(int index){
    int tSize;
    tSize = my.typeSize;
    int offset = index * tSize;
    adr curr;
    curr = my.head;
    curr = curr + offset;
    return curr;
};

int del@Array(){
    free(my.head);
    return 0;
};

int erace@LinkedList(){
    ListNode curr;
    curr = my.head;
    while(curr != NULL){
        ListNode temp;
        temp = curr.next;
        free(curr.data);
        free(curr);
        curr = temp;
    };
    free(my);
    return 0;
};

LinkedList reverse@LinkedList(){
    // flip all of the node pointers
    ListNode curr;
    curr = my.head;
    ListNode prev;
    prev = NULL;
    while(curr != NULL){
        ListNode temp;
        temp = curr.next;
        curr.next = prev;
        prev = curr;
        curr = temp;
    };
    my.head = prev;
    return my;
};

int extend@Array(int amount){
    adr curr;
    curr = my.head;
    int currSize;
    currSize = my.size;
    my.size = currSize + amount;

    int tSize;
    tSize = my.typeSize;

    amount = amount + currSize;
    amount = amount * tSize;

    my.head = realloc(curr, amount);
    if(my.head == NULL){
        panic("bad realloc\n");
    };
    return  amount;
};

class List {
    private adr head = malloc(typeSize * size);
    private int typeSize = typeSize;
    private int size = size;
    private int count = 0;
    private adr delFunc = delFunc;

    List init(int typeSize, * int size, * adr delFunc){
        if size == 0 {
            my.size = 1;
            my.head = malloc(typeSize);
        };
        return my;
    };

    int pushBack(adr value) {
        if my.count == my.size {
            my.size = my.size * 2;
            my.head = realloc(my.head, my.typeSize * my.size);
        };

        memcpy(my.head + (my.count * my.typeSize), value, my.typeSize);
        my.count = my.count + 1;
        return my.count;
    };

    generic get(int index) {
        if index >= my.count {
            return NULL;
        };

        return my.head + (index * my.typeSize);
    };

    generic popBack() {
        if my.count == 0 {
            return NULL;
        };

        my.count = my.count - 1;
        return my.head + (my.count * my.typeSize);
    };

    int getCount() {
        return my.count;
    };

    generic back() {
        return my.get(my.count - 1);
    };

    int forEach(adr func, * adr args) {
        for int i = 0; i < my.count; i = i + 1 {
            func(my.get(i), i, args);
        };
        return 0;
    };

    int set(int index, adr value) {
        if index >= my.count {
            return 1;
        };

        memcpy(my.head + (index * my.typeSize), value, my.typeSize);
        return 0;
    };

    int insert(int index, adr value) {
        if index > my.count {
            return 1;
        };

        if my.count == my.size {
            my.size = my.size * 2;
            my.head = realloc(my.head, my.typeSize * my.size);
        };
        
        memcpy(my.head + ((index + 1) * my.typeSize), my.head + (index * my.typeSize), my.typeSize * (my.count - index));
        memcpy(my.head + (index * my.typeSize), value, my.typeSize);
        my.count = my.count + 1;
    };

    int drop(int index) {
        if index >= my.count {
            return 1;
        };

        if my.delFunc != NULL {
            adr delFunc = my.delFunc;
            delFunc(my.get(index));
        };

        memcpy(my.head + (index * my.typeSize), my.head + ((index + 1) * my.typeSize), my.typeSize * (my.count - index));
        my.count = my.count - 1;
    };

    List map(int newTypeSize ,adr func, * adr args) {
        let result = new List(newTypeSize, my.count);
        for int i = 0; i < my.count; i = i + 1 {
            adr res = func(my.get(i), i, args);
            result.pushBack(res);
        };
        return result;
    };

    List filter(adr func, * adr args) {
        let result = new List(my.typeSize, my.count);
        for int i = 0; i < my.count; i = i + 1 {
            bool res = func(my.get(i), i, args);
            if res {
                result.pushBack(my.get(i));
            };
        };
        return result;
    };

    int del() {
        if my.delFunc != NULL
            my.forEach(my.delFunc);
        free(my.head);
        return 0;
    };
};