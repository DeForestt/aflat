.needs <asm>
.needs <std>

import {len, int_toStringL0, str_toInt} from "strings" under str;
import Function from "Utils/Functions";
import * from "io" under io;

class AsyncOutput {
    bool okay;
    any data;

    AsyncOutput init(const bool okay, const any data) {
        my.okay = okay;
        my.data = data;
        return my;
    };
};

class Thread {
    contract {
        private int pid;
    };
    int getPid();
};

class Process signs Thread {
    private adr foo;

    Process init(adr foo);
    int start(* adr args);
    bool isRunning();
};

class Pipe {
    int in;
    int out;
    int write(adr buf, int len);
    int read(adr buf, int len);
    Pipe init();
};

class Message{
    int len;
    adr body;

    public Message init(adr body);
    public int writePipe(Pipe pipe);
    private Message copy<<=>>(Message msg);
};

class MProcess signs Thread {
    private adr foo;
    private Pipe inbox;
    private Pipe outbox;

    public MProcess init(adr foo);
    int start(* adr args);
    private Process copy<<=>>();
    public bool input(adr message);
    public adr output();
    public int kill(){
        sys_kill(my.pid, 2);
    };
};

class FD {
  public int out;
  public int in;
};

safe dynamic pedantic class AsyncResult{
    private MProcess p = p;
    private int refcount = 1;

    AsyncResult init(const MProcess p){
        return my;
    };
    
    AsyncResult get() {
		my.refcount = my.refcount + 1;
		return my;
	};

    void del() {
        my.p.kill();
        const MProcess p = my.p;
        delete p;
    };

    public adr await() {
        return my.p.output();
    };

    public adr then(const adr func, * const any arg1, * const any arg2, * const any arg3, * const any arg4) {
        const adr res = my.p.output();
        return func(res, arg1, arg2, arg3, arg4);
    };

    public int kill() {
        return my.p.kill();
    };

    public int timeout(const int time, *const int nano) {
        // create a new process that waits for time and then kills the process

        const Process timeout = Process([const adr args]=>{
            const int[3] arg = args;
            sleep(arg[0], arg[1]);        
            if sys_kill(arg[2], 2) == 0 {
                io.print("timeout: killed process");
            };
            sys_exit(0);
        });
        const int[3] arg = {time, nano, my.p.getPid()};
        timeout.start(arg);
        return 0;
    };

    void endScope() {
        my.refcount = my.refcount - 1;
        if my.refcount == 0 {
            delete my;
        };
    };
};

class WorkArgs {
    public adr func = bar;
    public adr arg1 = arg1;
    public adr arg2 = arg2;
    public adr arg3 = arg3;
    public adr arg4 = arg4;

    WorkArgs init(const adr bar, *const adr arg1, *const adr arg2, *const adr arg3, *const adr arg4){
        return my;
    };

    adr run() {
        const adr func = my.func;
        return func(my.arg1, my.arg2, my.arg3, my.arg4);
    };
};

Message init@Message(const adr body){
    my.len = blockSize(body);
    my.body = body;
};

Process init@Process(const adr bar){
    my.foo = bar;
    my.pid = 0;
};

MProcess init@MProcess(const adr bar){
    my.foo = bar;
    my.pid = 0;
    my.inbox = new Pipe();
    my.outbox = new Pipe();
};

int start@Process(const adr args){
    my.pid = sys_fork();
    const adr bar = my.foo;
    if(my.pid == 0){
        bar(args);
        sys_exit(0);
    };
    return 0;
};

int start@MProcess(const adr args){
    my.pid = sys_fork();
    const adr bar = my.foo;
    if my.pid == 0 {
        bar(args, my.inbox, my.outbox);
        sys_exit(0);
    };
    return 0;
};

int getPid@Thread(){
    return my.pid;
};

bool isRunning@Process(){
    const int status = sys_kill(my.pid, 0); // 0 means no signal
    return status >= 0;
};



export Message readPipe(const Pipe pipe){
    const byte[4] header;
    pipe.read(header, 4);
    const int mlen = header as int;
    // read body from pipe
    const adr mbody = malloc(mlen);

    pipe.read(mbody, mlen);
    const Message m = Message(mbody);
    return m;
};

int writePipe@Message(const Pipe pipe){
    const int l = my.len;
    pipe.write(?l, 4);
    pipe.write(my.body, my.len);
    return 0;
};

bool input@MProcess(const adr message){
    const Message m = Message(message);
    const Pipe pipe = my.inbox;
    m.writePipe(pipe);
    return true;
};


adr output@MProcess(){
    const Pipe outbox = my.outbox;
    const Message m = readPipe(outbox);
    return m.body;
};

Pipe init@Pipe(){
    const FD fd = FD();
    sys_pipe(fd);
    my.out = fd.out;
    my.in = fd.in;
};

int read@Pipe(const adr buf, const int size){
    const int fd = my.out;
    const int ret = sys_read(fd, buf, size);
    return ret;
};

int write@Pipe(const adr buf, const int size){
    const int fd = my.in;
    const int ret = sys_write(fd, buf, size);
    return ret;
};

export int exit(const int status){
    sys_exit(status);
    return 0;
};

export int wait(const Thread p){
    sys_wait(p.getPid(), NULL, 0, NULL);
    return 0;
};

// Start a function as a concurrent process
export AsyncResult worker(const adr foo, *const any arg1, *const any arg2, *const any arg3, *const any arg4) {
    const MProcess p = new MProcess([const WorkArgs args, const Pipe __inbox, const Pipe outbox] => {
        const adr res = args.run();
        const Message msg = new Message(res);
        msg.writePipe(outbox);
    });
    const AsyncResult ret = new AsyncResult(p);
    const WorkArgs pArgs = new WorkArgs(foo, arg1, arg2, arg3, arg4);
    p.start(pArgs);
    return ret; 
};

export AsyncResult do(const Function foo) {
    const MProcess p = new MProcess([const Function func, const Pipe __inbox, const Pipe outbox] => {
        const adr res = func();
        const Message msg = new Message(res);
        msg.writePipe(outbox);
    });
    const let ret = new AsyncResult(p);
    p.start(foo);
    return ret;
};

export generic makeShared(const int size, * const adr constructor, * const any arg1, * const any arg2, * const any arg3, * const any arg4) {
    const adr val = mmap(NULL, size, 1 | 2, 1 | 32, -1, 0);
    if constructor != NULL {
        constructor(val, arg1, arg2, arg3, arg4);
    };
    return val;
};
