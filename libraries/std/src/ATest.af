.needs <std>
.needs <asm>
import {print, printInt, fPrint} from "io" under io;
import LinkedList from "Collections";
import String, string from "String";
import {printString, print} from "String" under str;

// keep track of passes and failures
int passes = 0;
int failures = 0;

int requires = 0;

export bool case(adr foo, adr _arg) {
	io.print("Test Case "); io.print(_arg); io.print(": ");

	bool result = foo(_arg);

	if result {
		io.print("OK\n", 'g');
        passes = passes + 1;
        io.print("\n----------------------------------------------------\n");
		return true;
	} else {
		io.print("FAIL\n", 'r');
        // print ' ' to stdErr
        sys_write(2, " ", 1);
        failures = failures + 1;
        io.print("\n----------------------------------------------------\n");
		return false;
	};
    requires = 0;
};

export bool require(bool result, string describe) {
    if result {
        passes = passes + 1;
        return true;
    } else {
        io.fPrint("\n\tTest require: %s: ", {describe});
        io.print("FAIL\n", 'r');
        // print ' ' to stdErr
        sys_write(2, " ", 1);
        failures = failures + 1;
        return false;
    };
    requires = requires + 1;
};

export int report() {
    io.print("Passed tests: "); io.printInt(passes); io.print("\n");
    io.print("Failed tests: "); io.printInt(failures); io.print("\n");

    for int i = 0; i < failures; i++ {
        io.print("-", 'r');
    };
    
    for int i = 0; i < passes; i++ {
        io.print("+", 'g');
    };
    io.print("\n");
    
    if failures > 0 {
        return 1;
    } else {
        return 0;
    };
};

class TestSuite {
    LinkedList cases = new LinkedList();
    LinkedList args = new LinkedList();

    String name = new String(name);

    TestSuite init(adr name) {
        return my;
    };

    int addCase(adr foo, adr _arg) {
        my.cases.append(foo);
        my.args.append(_arg);
        return 0;
    };

    int run() {
        io.print("\n\nRunning test suite: "); str.printString(my.name); io.print("\n\n\n");
        for int i = 0; i < my.cases.size(); i++ {
            adr foo = my.cases.get(i);
            adr _arg = my.args.get(i);
            foo(_arg);
        };
        return 0;
    };

};

class Mockable {
	private adr foo = foo;
	private adr mockImpl = NULL;
	private bool mock = false;
	private int callCount = 0;

	Mockable init(adr foo) {
		return my;
	};

	void mock(adr mockImpl) {
		my.mockImpl = mockImpl;
		my.mock = true;
	};

	void unMock() {
		my.mock = false;
		my.mockImpl = NULL;
	};

	private any _call(adr ref, * any arg1, * any arg2, * any arg3, * any arg4) {
		my.callCount = my.callCount + 1;
		let mockImpl = my.mockImpl;
		let foo = my.foo;
		if (my.mock) {
			return mockImpl(ref, arg1, arg2, arg3, arg4);
		} else {
			return foo(ref, arg1, arg2, arg3, arg4);
		};
	};

	bool wasCalled() {
		return my.callCount > 0;
	};

	int getCallCount() {
		return my.callCount;
	};

	void resetCallCount() {
		my.callCount = 0;
	};

	void reset() {
		my.resetCallCount();
		my.unMock();
	};
};